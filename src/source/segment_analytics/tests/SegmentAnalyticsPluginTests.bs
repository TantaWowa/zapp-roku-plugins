'@TestSuite [SAPT] SegmentAnalyticsPluginTests

namespace SAPT

'@BeforeEach
function beforeEach()
  config = {
    "write_key": "#w"
  }
  m.modelLocator = createObject("roSGNode", "ModelLocator")
  m.modelLocator.constants = {
    "applicasterDeviceId": "#a"
  }
  TU.setModelLocator(m.modelLocator)

  m.pluginData = new BasePlugin({ "configuration_json": config, "identifier": "segment_analytics_roku" })
  
  m.plugin = new SegmentAnalyticsPlugin(m.pluginData)
end function

'@AfterEach
function afterEach()
  TU.unsetModelLocator()
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'@It tests constructor
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

'@Test constructor
function constructor()
  m.assertEqual(m.plugin.id, "segment_analytics_roku")
  m.assertSubType(m.plugin.segmentTask, "SegmentAnalyticsTask")
  m.assertEqual(m.plugin.writeKey, "#w")
  m.assertEqual(m.plugin.applicasterDeviceId, "#a")
  m.assertNotInvalid(m.plugin.library)
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'@It tests start
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

'@Test start
function start()
  
  library = { "id" : "library" }
  m.plugin.library = library
  
  m.expectOnce(library, "init", [{
    "writeKey": "#w"
    "debug": true
    "queueSize": 3
    "retryLimit": 0
  }])
  m.expectOnce(m.plugin, "identify", ["Gigya"])

  m.plugin.start()
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'@It tests generateAuthProviderNameMap
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

'@Test some
function generateAuthProviderNameMap_some()
  config = {
    "write_key": "#w"
    "provider_1_id": "AdobePrimetime"
    "provider_1_name": "Adobe"
    "provider_2_id": "aws_cognito_auth_plugin"
    "provider_2_name": "Cognito"
    "provider_3_id": ""
    "provider_3_name": ""
  }

  pluginData = new BasePlugin({ "configuration_json": config, "identifier": "segment_analytics_roku" })

  map = m.plugin.generateAuthProviderNameMap(pluginData)
  
  m.assertEqual(map["AdobePrimetime"], "Adobe")
  m.assertEqual(map["aws_cognito_auth_plugin"], "Cognito")
end function

'@Test none
function generateAuthProviderNameMap_none()
  config = {
    "write_key": "#w"
    "provider_1_id": ""
    "provider_1_name": ""
    "provider_2_id": ""
    "provider_2_name": ""
    "provider_3_id": ""
    "provider_3_name": ""
  }

  pluginData = new BasePlugin({ "configuration_json": config, "identifier": "segment_analytics_roku" })

  m.assertEmpty(m.plugin.generateAuthProviderNameMap(pluginData))
  
end function

end namespace